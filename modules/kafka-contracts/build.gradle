plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}

group = 'com.advanced'
version = '1.0.0-SNAPSHOT'
description = 'Kafka Contracts'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

dependencies {
    implementation 'org.apache.avro:avro:1.11.3'
}

avro {
    stringType = 'String'
    fieldVisibility = 'PRIVATE'
    createSetters = false
    enableDecimalLogicalType = true
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-main-avro-java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

compileJava.dependsOn(generateAvroJava)

publishing {
    publications {
        contract(MavenPublication) {
            from components.java
            groupId = group
            artifactId = 'kafka-contracts'
            version = project.version
        }
    }
    repositories {
        maven {
            name = "artifactory"
            url = System.getenv('ARTIFACTORY_URL') ?: 'http://localhost:8092/repository/maven-snapshots/'
            allowInsecureProtocol = true
            credentials {
                username = project.findProperty("ARTIFACTORY_USER") ?: "admin"
                password = project.findProperty("ARTIFACTORY_PASSWORD") ?: "admin"
            }
        }
    }
}

tasks.named('jar') {
    enabled = true
}