import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.flywaydb.flyway' version '9.22.3'
}

group = 'com.advanced'
version = '1.0.0'
description = 'Transaction Service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven {
		url = System.getenv('ARTIFACTORY_URL') ?: 'http://localhost:8092/artifactory/libs-release-local/'
		allowInsecureProtocol = true
		credentials {
			username = System.getenv('ARTIFACTORY_USER') ?: 'person-service'
			password = System.getenv('ARTIFACTORY_PASSWORD') ?: 'Person_service1'
		}
	}
	mavenCentral()
	maven { url 'https://packages.confluent.io/maven/' }
}

ext {
	set('opentelemetryVersion', "2.16.0")
}

dependencies {
	implementation project(':transaction-api-contract')
	implementation 'com.advanced:kafka-contracts:1.0.7'

	// ------------------------------ SPRING ------------------------------
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
	implementation 'org.apache.shardingsphere:shardingsphere-jdbc:5.5.2'

	implementation 'io.confluent:kafka-avro-serializer:7.5.3'
	implementation 'org.apache.avro:avro:1.11.3'

	// ------------------------------ DATASOURCE ------------------------------
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.postgresql:postgresql'

	// ------------------------------ OTel ------------------------------
	implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter'

	// ------------------------------ METRIC ------------------------------
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// ------------------------------ DEVTOOLS ------------------------------
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// ------------------------------ UTIL ------------------------------
	implementation 'org.mapstruct:mapstruct:1.6.3'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	// ------------------------------ TEST ------------------------------
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:kafka'
	testImplementation 'org.apache.shardingsphere:shardingsphere-infra-database-testcontainers:5.5.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${opentelemetryVersion}"
	}
}

flyway {
	locations = ['filesystem:src/main/resources/db/migration']
}

tasks.register('flywayMigrateDs0', FlywayMigrateTask) {
	url = 'jdbc:postgresql://localhost:5433/transaction_db_0'
	user = 'transaction-service'
	password = 'password'
}

tasks.register('flywayMigrateDs1', FlywayMigrateTask) {
	url = 'jdbc:postgresql://localhost:5434/transaction_db_1'
	user = 'transaction-service'
	password = 'password'
}

tasks.named('build') {
	dependsOn(':transaction-api-contract:publish')
}

tasks.named('test') {
	useJUnitPlatform()
}
